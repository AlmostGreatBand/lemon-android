apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getProp(name) {
    return System.getenv(name) ?: rootProject.localprops[name] ?: project[name]
}

def genVersionCode() {
    return project.versionMajorApp * 100000 + project.versionMinorApp * 1000 + project.versionPatch.toInteger()
}

def genVersionName() {
    return "${project.versionMajorApp}.${project.versionMinorApp}.${project.versionPatch}"
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.agb.lemon_android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode genVersionCode()
        versionName genVersionName()

        buildConfigField "String", "API_BASE_URL", '"' + getProp('API_BASE_URL') + '"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    copy {
        from "../.github/hooks"
        into "../.git/hooks"
        fileMode 0777
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'

    def preference_version = "1.1.1"
    implementation "androidx.preference:preference-ktx:$preference_version"

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    def coroutines_adapter_version = "1.0.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:$coroutines_adapter_version"

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def recycler_version = "1.1.0"
    implementation "androidx.recyclerview:recyclerview:$recycler_version"

    def material_version = "1.3.0-alpha03"
    implementation "com.google.android.material:material:$material_version"

    def coroutines_version = "1.4.0-M1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation project(path: ':core')
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
}
